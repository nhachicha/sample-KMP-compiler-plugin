plugins {
    id("org.jetbrains.kotlin.jvm")
    id('org.jetbrains.kotlin.kapt')
    id("com.github.johnrengelman.shadow") version "5.2.0"
    id("maven-publish")
}

dependencies {
    compileOnly deps.kotlin.stdlib
    compileOnly deps.kotlin.compiler

    kapt deps.autoService.compiler
    compileOnly deps.autoService.annotations
}

shadowJar {
    configurations = [project.configurations.compile]
    // use the same artifact name, but within a different location
    archiveClassifier.set("")
    destinationDirectory = file("$buildDir/shaded")
    relocate "com.intellij", "org.jetbrains.kotlin.com.intellij"
}

def id = 'accessor-modifier-compiler-plugin'

publishing {
    publications {
        unshaded(MavenPublication) { // unshaded artifact for Kotlin/Native (Konan)
            artifactId "${id}-unshaded"
            from components.java
        }

        shaded(MavenPublication) { // shaded artifact for Koltin JVM
            artifactId id
            artifact shadowJar
        }
    }
}


compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
